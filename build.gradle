group 'com.github.lavenderx'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    ext {
        guavaVersion = '19.0'
        protostuffVersion = '1.4.4'
        slf4jVersion = '1.7.21'
        springVersion = '4.3.1.RELEASE'
        springbootVersion = '1.3.5.RELEASE'
        undertowVersion = '1.3.23.Final'
    }

    configurations {
        all {
            exclude group: 'commons-logging', module: 'commons-logging'

            resolutionStrategy {
                failOnVersionConflict()

                force "org.slf4j:slf4j-api:${slf4jVersion}"
                force "com.google.guava:guava:${guavaVersion}"
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile 'ch.qos.logback:logback-classic:1.1.7'
        compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        compile "com.google.guava:guava:${guavaVersion}"
        compile 'org.projectlombok:lombok:1.16.8'
        compile 'org.apache.commons:commons-collections4:4.1'

        testCompile 'junit:junit:4.12'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
