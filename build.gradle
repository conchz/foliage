group 'com.github.lavenderx'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    ext {
        guava_version = '19.0'
        javassist_version = '3.20.0-GA'
        protostuff_version = '1.4.4'
        slf4j_version = '1.7.21'
        spring_version = '4.3.1.RELEASE'
        springboot_version = '1.3.5.RELEASE'
        undertow_version = '1.3.23.Final'
    }

    configurations {
        all {
            exclude group: 'commons-logging', module: 'commons-logging'

            resolutionStrategy {
                failOnVersionConflict()

                force "org.slf4j:slf4j-api:$slf4j_version"
                force "com.google.guava:guava:$guava_version"
                force "org.javassist:javassist:$javassist_version"
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile 'ch.qos.logback:logback-classic:1.1.7'
        compile "org.slf4j:jcl-over-slf4j:$slf4j_version"
        compile "com.google.guava:guava:$guava_version"
        compile "org.javassist:javassist:$javassist_version"

        testCompile 'junit:junit:4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
